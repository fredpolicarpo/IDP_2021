plugins {
    id 'io.vertx.vertx-plugin' version '1.2.0'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'application'
}

repositories {
    mavenCentral()
}

def vertxVersion = "4.0.3"

def mainVerticleName = "com.fredpolicarpo.hello_consumer_microservice_message.HelloConsumer"
def launcherClassName = "io.vertx.core.Launcher"

def watchForChange = "src/**/*"
def doOnChange = "gradle shadowJar"

dependencies {
    implementation("io.vertx:vertx-rx-java:${vertxVersion}")
    implementation("io.vertx:vertx-web:${vertxVersion}")
    implementation group: 'io.vertx', name: 'vertx-infinispan', version: vertxVersion
}

application {
    mainClassName = launcherClassName
}

vertx {
    mainVerticle = mainVerticleName
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

shadowJar {
    archiveClassifier.set("fat")
    manifest {
        attributes('Main-Verticle': mainVerticleName)
    }
    mergeServiceFiles()
}

task runJar(type: JavaExec) {
    classpath = files(tasks.shadowJar)
    args = [
            "run",
            mainVerticleName,
            "--redeploy=$watchForChange",
            "--launcher-class=$launcherClassName",
            "--on-redeploy=$doOnChange",
            "-cluster",
            "-Djava.net.preferIPv4Stack=true"
    ]
}
